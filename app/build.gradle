apply plugin: 'com.android.application'

def date = new Date()
def formattedDate = date.format('yyMMddHHmm')
def versionCodeFromDate = formattedDate.toInteger()

Properties signingProperties = new Properties()
signingProperties.load(new FileInputStream(file(project.property("signing-secret.properties"))))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "radix.com.dotto"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode versionCodeFromDate
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(signingProperties['keystore.path'])
            storePassword signingProperties['keystore.password']
            keyAlias signingProperties['key.alias']
            keyPassword signingProperties['key.password']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // color picker
    compile('com.thebluealliance:spectrum:0.7.1', {
        exclude group: 'com.android.support'
    })

    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.1.1'

    // See https://github.com/firebase/FirebaseUI-Android#compatibility-with-firebase--google-play-services-libraries
    // this version is tied to the ui-auth version
    compile 'com.google.firebase:firebase-database:10.2.0'
    compile('com.firebaseui:firebase-ui-auth:1.2.0', {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    })

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'